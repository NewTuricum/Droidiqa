plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

def droidiqaVersion = "0.8.1"

def gitHubCredentials = rootProject.file("github.properties")
if (gitHubCredentials.exists()) {
    apply from: gitHubCredentials
} else {
    ext.githubUser = "unknown"
    ext.githubKey = "unknown"
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        generateStubs = true
    }
}

dokkaJavadoc.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
            includeNonPublics.set(false)
            suppressInheritedMembers.set(false)
        }
    }
    outputDirectory.set(new File("$buildDir/javadoc"))
}

task androidJavadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    archiveClassifier.set("javadoc")
    from("$buildDir/javadoc")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'ch.newturicum.libraries'
                version = droidiqaVersion
                artifact(tasks.getByName("androidJavadocJar"))
            }
            debug(MavenPublication) {
                from components.debug
                groupId = 'ch.newturicum.libraries'
                version = droidiqaVersion + "-debug"
                artifact(tasks.getByName("androidJavadocJar"))
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/NewTuricum/Droidiqa")
                credentials {
                    username = githubUser
                    password = githubKey
                }
            }
        }
    }
}


dependencies {
    def room_version = "2.3.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation "androidx.lifecycle:lifecycle-process:2.3.1"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.android.volley:volley:1.2.0'
    implementation "org.firestack:laksaj:1.1.0-RELEASE"

    kapt "androidx.room:room-compiler:$room_version"

    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}